plugins {
    id 'java-library'
    id 'io.github.goooler.shadow' version '8.1.8'
    id 'net.neoforged.moddev' version '1.0.0'
}

group 'org.lovetropics'
base {
    archivesName = 'serverpacklocator'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.tterrag.com/' }
    mavenLocal()
}

neoForge {
    version = project.neo_version

    parchment {
        minecraftVersion = minecraft_version
        mappingsVersion = parchment_version
    }

    mods {
        spl {
            modSourceSets = [sourceSets.main]
        }
    }

    runs {
        client {
            client()
        }

        server {
            server()
            programArgument '--nogui'
        }
    }
}

configurations {
    shadow
}

dependencies {
    shadow(implementation('io.netty:netty-codec-http:4.1.82.Final')) {
        transitive = false
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(
                'Automatic-Module-Name': 'serverpacklocator'
        )
    }

    archiveClassifier = 'slim'
}

tasks.named('shadowJar', Jar).configure {
    // remove '-all' from shadow jar file
    archiveClassifier = ''
    configurations = [project.configurations.shadow]

    ['http', 'rtsp', 'spdy'].forEach {
        relocate "io.netty.handler.codec.$it", "cpw.mods.forge.serverpacklocator.repack.io.netty.handler.codec.$it"
    }
}

assemble.dependsOn('shadowJar')
