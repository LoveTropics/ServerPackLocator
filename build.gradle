plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7+'
    id 'org.ajoberstar.grgit' version '3.1.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

apply plugin: 'maven-publish'
apply plugin: 'java-library'
group 'net.minecraftforge'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://files.minecraftforge.net/maven' }
    maven { url 'https://maven.tterrag.com/' }
}

test {
    useJUnitPlatform()
}

ext.forge_version = "47.1.3"

ext.sharedManifest = manifest {
    attributes(['Class-Path': 'forge.jar',
     'Main-Class': 'net.minecraftforge.server.ServerMain',
     'ServerLaunchArgs': "--gameDir . --launchTarget fmlserver --fml.forgeVersion ${forge_version} --fml.mcpVersion 20210706.113038 --fml.mcVersion 1.17.1 --fml.forgeGroup net.minecraftforge"
    ],
    )
    attributes(
            [
             "Automatic-Module-Name"   : "serverpacklocator",
             "Specification-Title"     : "serverpacklocator",
             "Specification-Vendor"    : "cpw",
             "Specification-Version"   : "1", // Currently version 3 of the forgespi specification
             "Implementation-Title"    : project.name,
             "Implementation-Version"  : "${version}+${System.getenv("BUILD_NUMBER") ?: 0}+${grgit.head().abbreviatedId}",
             "Implementation-Vendor"   : "cpw",
             "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
             "Git-Commit"              : grgit.head().abbreviatedId,
             "Git-Branch"              : grgit.branch.current().getName()],
            "cpw/mods/forge/serverpacklocator/")
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

minecraft {
    mappings channel: 'official', version: '1.20.1'
}

configurations {
    shadow
}

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.1.3"

    shadow(implementation('io.netty:netty-codec-http:4.1.82.Final')) {
        transitive = false
    }

    implementation("cpw.mods:modlauncher:9.0.7")
    implementation("com.google.code.gson:gson:2.8.0")
    implementation("org.apache.logging.log4j:log4j-api:2.17.0")
    implementation("com.electronwill.night-config:core:3.6.0")
    implementation("com.electronwill.night-config:toml:3.6.0")
    implementation("org.apache.maven:maven-artifact:3.6.3")
    testImplementation("org.junit.jupiter:junit-jupiter:5.4.2")
}

tasks.named('jar', Jar).configure {
    manifest = project.manifest {
        from sharedManifest
    }

    archiveClassifier = 'slim'
}

tasks.named('shadowJar', Jar).configure {
    // remove '-all' from shadow jar file
    archiveClassifier = ''
    configurations = [project.configurations.shadow]
    finalizedBy('reobfShadowJar')

    ['http', 'rtsp', 'spdy'].forEach {
        relocate "io.netty.handler.codec.$it", "cpw.mods.forge.serverpacklocator.repack.io.netty.handler.codec.$it"
    }
}

reobf {
    shadowJar {}
}

compileJava {
    options.compilerArgs << "-XDignore.symbol.file"
}